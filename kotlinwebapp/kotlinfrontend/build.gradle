group 'com.personal.demo'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.kotlin.frontend'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    compile("org.jetbrains.kotlinx:kotlinx-html-js:$html_version")
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "${projectDir}/web/js/output.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
    kotlinOptions.verbose = true
    doLast {
        //Copy kotlin library files to the web directory
        // Copy kotlin.js and kotlin-meta.js from jar into web directory
        configurations.compile.each { File file ->
            copy {
                includeEmptyDirs = false

                from zipTree(file.absolutePath)
                into "${projectDir}/web/js/lib"
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
        //Copy static resources to the web directory
        copy {
            includeEmptyDirs = false
            from "${project.buildDir.path}/resources"
            into "${projectDir}/web"
        }
    }
    doFirst {
        delete "${projectDir}/web"
    }
}

compileTestKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "${projectDir}/tests/${project.name}-tests.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
//    kotlinOptions.moduleName = project.name + "-test"
    kotlinOptions.main = "call"
}

kotlinFrontend {
// uncomment this to specify exact nodejs version (bundle will be downloaded)
    downloadNodeJsVersion = "latest"

    sourceMaps = false

    npm {
        replaceVersion("kotlinx-html-js", "1.1.0")
        replaceVersion("kotlinx-html-shared", "1.1.0")
        replaceVersion("kotlin-js-library", "1.1.0")

        dependency("firebase")
        dependency("style-loader")
        dependency("react")
        dependency("react-dom")
        dependency("react-router")
        dependency("jquery")
        dependency("react-markdown")

        devDependency("css-loader")
        devDependency("style-loader")
        devDependency("babel-loader")
        devDependency("babel-core")
        devDependency("karma")
    }

    webpackBundle {
        bundleName = "main"
        sourceMapEnabled = true // enable/disable source maps
        publicPath = "/frontend/" // web prefix
        contentPath = file('web') // a file that represents a directory to be served by dev server)
        host = "localhost" // dev server host
        port = 9090   // dev server port
        proxyUrl = "http://localhost:8080" // URL to be proxied, useful to proxy backend webserver
        mode = "production"
    }

    define "PRODUCTION", false
    define "X", false
}

sourceSets {
    main {
        kotlin.srcDirs = [ 'src/main/kotlin' ]
        java.srcDirs = [ 'src/main/java' ]
        resources.srcDirs += "src/main/resources"
        output.resourcesDir = "build/resources"
    }
    test {
        kotlin.srcDirs = [ 'src/test/kotlin' ]
        java.srcDirs = [ 'src/test/java' ]
        resources.srcDirs += "src/test/resources"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
